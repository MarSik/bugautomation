import org.marsik.bugautomation.facts.BugzillaBug;
import org.marsik.bugautomation.facts.TrelloBoard;
import org.marsik.bugautomation.facts.TrelloCard;
import org.marsik.bugautomation.facts.TrelloLabel;
import org.marsik.bugautomation.facts.Bug;
import org.marsik.bugautomation.services.BugzillaActions;
import org.marsik.bugautomation.services.TrelloActions;
import org.marsik.bugautomation.facts.BugzillaBugFlag;

global TrelloActions trello;
global BugzillaActions bugzilla;

rule "newBug"
  when
    $bz: BugzillaBug($bug : bug, $user : assignedTo)
    $board: TrelloBoard(name == "Test Sprint automation")
    not(exists TrelloCard(bug == $bug, board == $board))
  then
    trello.createCard($board, "TODO", $bz, $user);
end

rule "orderBacklog"
  when
    $bz: BugzillaBug($bug1 : bug, priority != null, $priority : priority)
    BugzillaBug($bug2 : bug, priority != null, priority > $priority, id != $bz.id)

    $board: TrelloBoard(name == "Test Sprint automation")

    $card1: TrelloCard(bug == $bug1, board == $board, status == "todo", $pos : pos)
    $card2: TrelloCard(bug == $bug2, board == $board, status == "todo", pos > $pos)
  then
    trello.switchCards($card1, $card2);
end

rule "bugAssignment"
  when
    $bz: BugzillaBug($bug : bug, $user : assignedTo)
    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, $user not memberOf assignedTo)
  then
    trello.assignCard($card, $user);
end

rule "closedBug"
  when
    $bz: BugzillaBug($bug : bug, $user : assignedTo, status == "closed")
    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, status != "done")
  then
    trello.moveCard($card, $board, "Done");
end

rule "doneBug"
  when
    $bz: BugzillaBug($bug : bug, status == "modified" || status == "on_qe")
    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, status != "done", status != "documentation")
  then
    trello.moveCard($card, $board, "Documentation");
end

rule "unknownBug"
  when
    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard($bug : bug, bug != null, board == $board, status != "done")
    not(exists BugzillaBug($bug == bug))
  then
    trello.moveCard($card, $board, "Done");
end

rule "openBug"
  when
    $bz: BugzillaBug($bug : bug, $user : assignedTo, status == "new" || status == "assigned" || status == "post")
    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, status == "done")
  then
    trello.moveCard($card, $board, "TODO");
end

rule "modifiedBug"
  when
    $bz: BugzillaBug($bug : bug, $user : assignedTo, status == "post")
    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, status == "todo")
  then
    trello.moveCard($card, $board, "In Progress");
end

rule "blockerBug"
  when
    $bz: BugzillaBug($bug : bug, status != "closed", $flags: flags)
    BugzillaBugFlag(flag == "blocker+") from $flags
    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, $labels: labels)
    not(exists TrelloLabel(name == "blocker") from $labels)
  then
    trello.assignLabelToCard($card, "blocker");
end

rule "notABlockerBug"
  when
    $bz: BugzillaBug($bug : bug, status != "closed", $flags: flags)
    not (exists BugzillaBugFlag(flag == "blocker+") from $flags)
    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, $labels: labels)
    $label: TrelloLabel(name == "blocker") from $labels
  then
    trello.removeLabelFromCard($card, $label);
end

rule "failedQE"
  when
    $bz: BugzillaBug($bug : bug, status != "closed", "failedqe" memberOf keywords)

    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, $labels: labels)
    not(exists TrelloLabel(name == "failedqe") from $labels)
  then
    trello.assignLabelToCard($card, "failedqe");
end

rule "notFailedQEBug"
  when
    $bz: BugzillaBug($bug : bug, status != "closed", "failedqe" not memberOf keywords)

    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, $labels: labels)
    $label: TrelloLabel(name == "failedqe") from $labels
  then
    trello.removeLabelFromCard($card, $label);
end

rule "missingZStreamFlags"
  when
    $bz: BugzillaBug($bug : bug, status != "closed", "zstream" memberOf keywords, $flags: flags)
    not(exists BugzillaBugFlag(flag == "ovirt-4.0.z+") from $flags)
    not(exists BugzillaBugFlag(flag == "ovirt-3.6.z+") from $flags)
    not(exists BugzillaBugFlag(flag == "rhevm-4.0.z+") from $flags)
    not(exists BugzillaBugFlag(flag == "rhevm-3.6.z+") from $flags)

    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, $labels: labels)
    not(exists TrelloLabel(name == "flags missing") from $labels)
  then
    trello.assignLabelToCard($card, "flags missing");
end

rule "okZStreamFlags"
  when
    $bz: BugzillaBug($bug : bug, "zstream" memberOf keywords, $flags: flags)
    ((BugzillaBugFlag(flag == "ovirt-4.0.z+") from $flags)
    || (BugzillaBugFlag(flag == "ovirt-3.6.z+") from $flags)
    || (BugzillaBugFlag(flag == "rhevm-4.0.z+") from $flags)
    || (BugzillaBugFlag(flag == "rhevm-3.6.z+") from $flags))

    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, $labels: labels)
    $label: TrelloLabel(name == "flags missing") from $labels
  then
    trello.removeLabelFromCard($card, $label);
end

rule "notAZStream"
  when
    $bz: BugzillaBug($bug : bug, "zstream" not memberOf keywords)

    $board: TrelloBoard(name == "Test Sprint automation")
    $card: TrelloCard(bug == $bug, board == $board, $labels: labels)
    $label: TrelloLabel(name == "flags missing") from $labels
  then
    trello.removeLabelFromCard($card, $label);
end
